t←'(1 2)×(3+(3÷4))×(1+2)÷8'

⍝ Parenthesis depth changes
Di ← {1 ¯1 0['()'⍳⍵]}
Do ← -⌿'()'∘.=⊢
Df ← '('∘= - =∘')'
'turtle' 'joy' 'cmpx'⎕cy'dfns'
(y n)←(⊢⍴⍨100×⍴)⍤⎕VR¨'turtle' 'joy'
cmpx'D',¨'iof',¨⊂' y'
⍝ Di y → 9.0E¯4 |   0% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
⍝ Do y → 7.3E¯4 | -19% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕       
⍝ Df y → 3.4E¯4 | -63% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕                         
cmpx'D',¨'iof',¨⊂' n'
⍝ Di n → 1.4E¯3 |   0% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕ 
⍝ Do n → 1.5E¯3 |  +3% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
⍝ Df n → 6.8E¯4 | -54% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕                      

⍝ Balanced?
Ba ← (∧/0≤+\)∧0=+/
Bn ← (¯1∊+\)⍱0≠+/
yd←Df y
nd←Df n
cmpx'Ba' 'Bn',¨⊂' yd'
⍝ Ba yd → 1.2E¯3 |  0% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕ 
⍝ Bn yd → 1.3E¯3 | +2% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
cmpx'Ba' 'Bn',¨⊂' nd'
⍝ Ba nd → 2.0E¯3 |   0% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
⍝ Bn nd → 1.5E¯3 | -25% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕          
Bns ← (¯1∘∊⍱0≠⊢/)+\
cmpx'Bn' 'Bns',¨⊂' yd'
⍝ Bn yd  → 1.6E¯3 |   0% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
⍝ Bns yd → 1.3E¯3 | -18% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕       
cmpx'Bn' 'Bns',¨⊂' nd'
⍝ Bn nd  → 1.6E¯3 |   0% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
⍝ Bns nd → 1.4E¯3 | -12% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕    
F ← (¯1∘∊⍱0≠⊢/)'('∘=+\⍤-=∘')'

⍝ Find and Replace
Re  ← ''≡'\(\)'⎕R''⍣≡⍤∩∘'()'
Re0 ← ''≡'()'⎕R''⍠'Regex'0⍣≡⍤∩∘'()'
Fi  ← ''≡(⊢(/⍨)¯1(⊢⍱⌽)'()'∘⍷)⍣≡⍤∩∘'()'
cmpx'Re' 'Re0' 'Fi',¨⊂' y'
⍝ Re y  → 3.5E¯3 |   0% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
⍝ Re0 y → 2.0E¯3 | -43% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕                 
⍝ Fi y  → 2.0E¯3 | -44% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕                  
cmpx'Re' 'Re0' 'Fi',¨⊂' n'
⍝ Re n  → 6.1E¯3 |   0% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
⍝ Re0 n → 3.3E¯3 | -47% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕                   
⍝ Fi n  → 3.0E¯3 | -52% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕                    

⍝ Abusing APL's Parser
Nd ← {0::0 ⋄ (⍎'{} '['()'⍳⍵])/1}
Np ← {0::0 ⋄ ⊃∊⍎⍕1,1,¨⍵∩'()'}
