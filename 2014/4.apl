⍝ Compare adjacent characters:
A←{(' '=⊃⍵)↓⍵/⍨2∨/0,⍨' '≠⍵}
B←' '∘(=∘⊃↓⊢⊢⍤/⍨2∨/0,⍨≠)     ⍝ tacit generalisation
t←∘.{⍺,(40↑⎕A)[?1,1,⍨1e3⍴40],⍵}⍨0 7↑¨' '
)copy dfns cmpx
cmpx 'A¨t' 'B¨t'
⍝  A¨t → 5.6E¯6 |   0% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕         
⍝  B¨t → 7.1E¯6 | +27% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕

⍝ Cyclic rotation:
C←' '∘(1↓,⊢⍤/⍨1(⊢∨⌽)0,≠)
D←' '∘(=∘⊃↓⊢⊢⍤/⍨1↓1(⊢∨⌽)0,≠)  ⍝ conditionally remove instead of
E←' '∘{1↓(m∨1⌽m←0,⍺≠⍵)/⍺,⍵}
F←' '∘{¯1↓(m∨¯1⌽m←0,⍨⍺≠⍵)/⍵,⍺} ⍝ appending is faster than prepending
G←{⍵/⍨¯1↓(∨\∧⊢∨1⌽⊢)0,⍨' '≠⍵}
H←{⍵/⍨({⌽∨\⌽⍵}∧∨\∧⊢∨1⌽⊢)' '≠⍵}
I←{⍵/⍨m∨1⌽m><\m←' '≠⍵}
)copy dfns iotag
cmpx ('C'iotag'I'),¨⊂'¨t'
⍝  C¨t → 7.5E¯6 |   0% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕              
⍝  D¨t → 9.0E¯6 | +19% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕         
⍝  E¨t → 7.9E¯6 |  +4% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕             
⍝  F¨t → 7.3E¯6 |  -4% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕               
⍝  G¨t → 8.7E¯6 | +15% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕          
⍝  H¨t → 1.2E¯5 | +54% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
⍝  I¨t → 6.0E¯6 | -20% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕                   

⍝ Regex:
J←'^ *| *$' ' +'⎕R'' ' '         ⍝ treat leading/trailing separately from multiple
K←'^ +| +$| +(?= )'⎕R''          ⍝ specify all to be removed using
L←'[^ ] (?=.*[^ ])' ' '⎕R'&' ''  ⍝ exempt spaces to be kept, removing all other
cmpx'J¨t' 'K¨t' 'L¨t'
⍝  J¨t → 5.5E¯4 |    0% ⎕⎕⎕⎕⎕⎕⎕                                 
⍝  K¨t → 4.8E¯4 |  -14% ⎕⎕⎕⎕⎕⎕                                  
⍝  L¨t → 3.1E¯3 | +459% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕

⍝ Split and join:
M←{⊃(⊣,' ',⊢)/⍵⊆⍨' '≠⍵}  ⍝ fails on ''
N←{¯1↓∊,∘' '¨⍵⊆⍨' '≠⍵}   ⍝ fix that
O←' '∘{¯1↓∊,∘⍺¨⍵⊆⍨⍺≠⍵}   ⍝ generalisation
P←¯1↓' '∘(∊⊣,¨⍨≠⊆⊢)      ⍝ tacit form
cmpx('M'iotag'P'),¨⊂'¨t'
⍝  M¨t → 6.7E¯4 |   0% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
⍝  N¨t → 2.8E¯4 | -58% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕                       
⍝  O¨t → 2.7E¯4 | -61% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕                        
⍝  P¨t → 2.3E¯4 | -65% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕                          

⍝ Using ⍷:
Q←{⍵/⍨(0 0∘⍷<∨\∧∘⌽∨\∘⌽)' '≠⍵}
R←{⍵/⍨~'  '⍷⍵}{(∨\' '≠⍵)/⍵}∘⌽⍣2  ⍝ use idiom twice
S←{({- ⊃⌽⍵}↓⍵/⍨1 1∘⍷⍱∧\)' '=⍵}
cmpx'Q¨t' 'R¨t' 'S¨t'
⍝  Q¨t → 8.5E¯5 |   0% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
⍝  R¨t → 2.3E¯5 | -73% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕                             
⍝  S¨t → 8.5E¯5 |   0% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕

cmpx 'AIKPR',¨⊂'¨t'
⍝  A¨t → 6.1E¯6 |     0%                                         
⍝  I¨t → 6.0E¯6 |    -2%                                         
⍝  K¨t → 5.0E¯4 | +8052% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
⍝  P¨t → 2.4E¯4 | +3754% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕                     
⍝  R¨t → 2.1E¯5 |  +248% ⎕⎕                                      

cmpx 'AI',¨⊂'¨t'
⍝  A¨t → 5.5E¯6 |   0% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕      
⍝  I¨t → 6.5E¯6 | +17% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
