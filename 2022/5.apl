{⍳⍵}5
⍝ 1 2 3 4 5
{∘.+⍨⍳⍵}5
⍝ 2 3 4 5  6
⍝ 3 4 5 6  7
⍝ 4 5 6 7  8
⍝ 5 6 7 8  9
⍝ 6 7 8 9 10
{⍵<∘.+⍨⍳⍵}5
⍝ 0 0 0 0 1
⍝ 0 0 0 1 1
⍝ 0 0 1 1 1
⍝ 0 1 1 1 1
⍝ 1 1 1 1 1
{1+⍵<∘.+⍨⍳⍵}5
⍝ 1 1 1 1 2
⍝ 1 1 1 2 2
⍝ 1 1 2 2 2
⍝ 1 2 2 2 2
⍝ 2 2 2 2 2
{' ⎕'[1+⍵<∘.+⍨⍳⍵]}5
⍝     ⎕
⍝    ⎕⎕
⍝   ⎕⎕⎕
⍝  ⎕⎕⎕⎕
⍝ ⎕⎕⎕⎕⎕
F←{' ⎕'[1+⍵<∘.+⍨⍳⍵]}
F 5
⍝     ⎕
⍝    ⎕⎕
⍝   ⎕⎕⎕
⍝  ⎕⎕⎕⎕
⍝ ⎕⎕⎕⎕⎕
∘.<⍨⍳5
⍝ 0 1 1 1 1
⍝ 0 0 1 1 1
⍝ 0 0 0 1 1
⍝ 0 0 0 0 1
⍝ 0 0 0 0 0
∘.≥⍨⍳5
⍝ 1 0 0 0 0
⍝ 1 1 0 0 0
⍝ 1 1 1 0 0
⍝ 1 1 1 1 0
⍝ 1 1 1 1 1
⌽∘.≥⍨⍳5
⍝ 0 0 0 0 1
⍝ 0 0 0 1 1
⍝ 0 0 1 1 1
⍝ 0 1 1 1 1
⍝ 1 1 1 1 1
G←{' ⎕'[1+⌽∘.≥⍨⍳⍵]}
G 5
⍝     ⎕
⍝    ⎕⎕
⍝   ⎕⎕⎕
⍝  ⎕⎕⎕⎕
⍝ ⎕⎕⎕⎕⎕
(⍳5)∘.≥(⌽⍳5)
⍝ 0 0 0 0 1
⍝ 0 0 0 1 1
⍝ 0 0 1 1 1
⍝ 0 1 1 1 1
⍝ 1 1 1 1 1
∘.≥∘⌽⍨⍳5
⍝ 0 0 0 0 1
⍝ 0 0 0 1 1
⍝ 0 0 1 1 1
⍝ 0 1 1 1 1
⍝ 1 1 1 1 1
G2←{' ⎕'[1+∘.≥∘⌽⍨⍳⍵]}
G2 5
⍝     ⎕
⍝    ⎕⎕
⍝   ⎕⎕⎕
⍝  ⎕⎕⎕⎕
⍝ ⎕⎕⎕⎕⎕
'cmpx'⎕CY'dfns'
cmpx'G 1000' 'G2 1000'
⍝   G 1000  → 6.5E¯4 |   0% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
⍝   G2 1000 → 3.5E¯4 | -47% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕                   
G3←{⎕IO←0 ⋄ ' ⎕'[∘.≥∘⌽⍨⍳⍵]}
cmpx'G2 1000' 'G3 1000'
⍝   G2 1000 → 3.5E¯4 |   0% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
⍝   G3 1000 → 1.7E¯4 | -52% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕                    
3/'⎕'
⍝ ⎕⎕⎕
2 3/¨'⎕'
⍝ ┌──┬───┐
⍝ │⎕⎕│⎕⎕⎕│
⍝ └──┴───┘
(⍳5)/¨'⎕'
⍝ ┌─┬──┬───┬────┬─────┐
⍝ │⎕│⎕⎕│⎕⎕⎕│⎕⎕⎕⎕│⎕⎕⎕⎕⎕│
⍝ └─┴──┴───┴────┴─────┘
↑(⍳5)/¨'⎕'
⍝ ⎕    
⍝ ⎕⎕   
⍝ ⎕⎕⎕  
⍝ ⎕⎕⎕⎕ 
⍝ ⎕⎕⎕⎕⎕
⌽↑(⍳5)/¨'⎕'
⍝     ⎕
⍝    ⎕⎕
⍝   ⎕⎕⎕
⍝  ⎕⎕⎕⎕
⍝ ⎕⎕⎕⎕⎕
{⌽↑(⍳⍵)/¨'⎕'}5
⍝     ⎕
⍝    ⎕⎕
⍝   ⎕⎕⎕
⍝  ⎕⎕⎕⎕
⍝ ⎕⎕⎕⎕⎕
('⎕'/¨⍨⍳)5
⍝ ┌─┬──┬───┬────┬─────┐
⍝ │⎕│⎕⎕│⎕⎕⎕│⎕⎕⎕⎕│⎕⎕⎕⎕⎕│
⍝ └─┴──┴───┴────┴─────┘
(↑'⎕'/¨⍨⍳)5
⍝ ⎕    
⍝ ⎕⎕   
⍝ ⎕⎕⎕  
⍝ ⎕⎕⎕⎕ 
⍝ ⎕⎕⎕⎕⎕
(⌽⍤↑'⎕'/¨⍨⍳)5
⍝     ⎕
⍝    ⎕⎕
⍝   ⎕⎕⎕
⍝  ⎕⎕⎕⎕
⍝ ⎕⎕⎕⎕⎕
H←⌽⍤↑'⎕'/¨⍨⍳
('⎕'/⍤0⍨⍳)5
⍝ ⎕    
⍝ ⎕⎕   
⍝ ⎕⎕⎕  
⍝ ⎕⎕⎕⎕ 
⍝ ⎕⎕⎕⎕⎕
H2←⌽'⎕'/⍤0⍨⍳
H2 5
⍝     ⎕
⍝    ⎕⎕
⍝   ⎕⎕⎕
⍝  ⎕⎕⎕⎕
⍝ ⎕⎕⎕⎕⎕
cmpx'H 1000' 'H2 1000'
⍝   H 1000  → 8.8E¯4 |  0% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕  
⍝   H2 1000 → 9.2E¯4 | +4% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
3/'⎕'
⍝ ⎕⎕⎕
¯2 3/¨'⎕'
⍝ ┌──┬───┐
⍝ │  │⎕⎕⎕│
⍝ └──┴───┘
¯2 3/'⎕'
⍝   ⎕⎕⎕
⍳5
⍝ 1 2 3 4 5
5-⍳5
⍝ 4 3 2 1 0
(⍳5)-5
⍝ ¯4 ¯3 ¯2 ¯1 0
(⍳-⊢)5
⍝ ¯4 ¯3 ¯2 ¯1 0
(⍳⊣⊢)5
⍝ 1 2 3 4 5
(⍳(-,⊣)⊢)5
⍝ ¯4 ¯3 ¯2 ¯1 0 1 2 3 4 5
(⍳(-,⍤0⊣)⊢)5
⍝ ¯4 1
⍝ ¯3 2
⍝ ¯2 3
⍝ ¯1 4
⍝  0 5
('⎕'/⍤1⍨⍳(-,⍤0⊣)⊢)5
⍝     ⎕
⍝    ⎕⎕
⍝   ⎕⎕⎕
⍝  ⎕⎕⎕⎕
⍝ ⎕⎕⎕⎕⎕
H3←'⎕'/⍤1⍨⍳(-,⍤0⊣)⊢
cmpx'H2 1000' 'H3 1000'
⍝   H2 1000 → 7.2E¯4 |   0% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
⍝   H3 1000 → 5.1E¯4 | -30% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕            
]defs
⍝  F ← {' ⎕'[1+⍵<∘.+⍨⍳⍵]}      
⍝  G ← {' ⎕'[1+⌽∘.≥⍨⍳⍵]}       
⍝ G2 ← {' ⎕'[1+∘.≥∘⌽⍨⍳⍵]}      
⍝ G3 ← {⎕IO←0 ⋄ ' ⎕'[∘.≥∘⌽⍨⍳⍵]}
⍝  H ← ⌽⍤↑'⎕'/¨⍨⍳              
⍝ H2 ← ⌽'⎕'/⍤0⍨⍳               
⍝ H3 ← '⎕'/⍤1⍨⍳(-,⍤0⊣)⊢        
cmpx'F 1000' 'G3 1000' 'H3 1000'
⍝   F 1000  → 1.1E¯3 |   0% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
⍝   G3 1000 → 2.1E¯4 | -82% ⎕⎕⎕⎕⎕⎕⎕                                 
⍝   H3 1000 → 6.7E¯4 | -40% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕                
cmpx'G3 1000' 'H3 1000'
⍝   G3 1000 → 5.8E¯4 |    0% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕                            
⍝   H3 1000 → 1.9E¯3 | +221% ⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕⎕
